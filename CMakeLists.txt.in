##########################################################################
# Copyright (C) 2007-2009 Douglas Gregor <doug.gregor@gmail.com>         #
# Copyright (C) 2007-2009 Troy Straszheim <troy@resophonic.com>          #
# Copyright (C) 2010-2011 Daniel Pfeifer <daniel@pfeifer-mail.de>        #
#                                                                        #
# Distributed under the Boost Software License, Version 1.0.             #
# See accompanying file LICENSE_1_0.txt or copy at                       #
#   http://www.boost.org/LICENSE_1_0.txt                                 #
##########################################################################

cmake_minimum_required(VERSION 2.8.4 FATAL_ERROR)
project(Boost)

##########################################################################
# Config Information                                                     #
##########################################################################

set(BOOST_CONFIG_VERSION "1.46.1")

file(WRITE "${PROJECT_BINARY_DIR}/BoostConfigVersion.cmake"
  "set(PACKAGE_VERSION \"${BOOST_CONFIG_VERSION}\")\n"
  "set(PACKAGE_VERSION_EXACT TRUE)\n"
  )

file(WRITE "${PROJECT_BINARY_DIR}/BoostConfig.cmake"
  "set(Boost_FOUND TRUE)\n"
  "set(Boost_INCLUDE_DIRS \"${CMAKE_BINARY_DIR}/include\")\n"
  "set(Boost_LIBRARY_DIRS \"${CMAKE_BINARY_DIR}/lib\")\n"
  "set(Boost_RESOURCE_PATH \"${PROJECT_SOURCE_DIR}/src/cmake/resources\")\n"
  "set(Boost_MODULE_PATH \"${PROJECT_SOURCE_DIR}/src/cmake/modules\")\n"
  "set(BOOSTBOOK_CATALOG \"${PROJECT_BINARY_DIR}/src/boostbook/catalog.xml\")\n"
  "set(BOOSTBOOK_XSL_DIR \"${PROJECT_SOURCE_DIR}/src/boostbook/xsl\")\n"
  "set(Boost_USE_FILE \"\${Boost_MODULE_PATH}/UseBoost.cmake\")\n"
  "set(Boost_DEV_FILE \"\${Boost_MODULE_PATH}/UseBoostDev.cmake\")\n"
  )

set(Boost_DIR ${PROJECT_BINARY_DIR} CACHE PATH "")
find_package(Boost NO_MODULE)
include("${Boost_DEV_FILE}")

##########################################################################
# Building and Testing                                                   #
##########################################################################

enable_testing()

# make visual studio look nice
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# Put the libaries and binaries that get built into directories at the
# top of the build tree rather than in hard-to-find leaf
# directories. This simplifies manual testing and the use of the build
# tree rather than installed Boost libraries.
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)


set(BOOST_PROJECTS_ALL)

##########################################################################
@BOOST_MODULE_ADD_SUBDIRECTORIES@

##########################################################################

list(REMOVE_DUPLICATES BOOST_PROJECTS_ALL)
list(SORT BOOST_PROJECTS_ALL)

# function to set CPACK_COMPONENT_*
function(set_cpack_component name)
  string(TOUPPER "CPACK_COMPONENT_${name}" variable)
  set(${variable} ${ARGN} PARENT_SCOPE)
endfunction(set_cpack_component)

set(CPACK_COMPONENTS_ALL)

foreach(project ${BOOST_PROJECTS_ALL})
  set(has_develop ${BOOST_PROJECT_${project}_HAS_DEVELOP})
  set(has_runtime ${BOOST_PROJECT_${project}_HAS_RUNTIME})
  set(has_manual  ${BOOST_PROJECT_${project}_HAS_MANUAL})
  set(is_tool     ${BOOST_PROJECT_${project}_TOOL})
  set(name        "${BOOST_PROJECT_${project}_NAME}")
  set(description "${BOOST_PROJECT_${project}_DESCRIPTION}")
  set(group       "boost_${project}")

  if(NOT is_tool)
    set(name "${name} Library")
  endif()

  set_cpack_component(GROUP_${group}_DISPLAY_NAME "${name}")
  set_cpack_component(GROUP_${group}_DESCRIPTION "${description}")

  set_cpack_component(${project}_DEVELOP_GROUP "${group}")
  set_cpack_component(${project}_RUNTIME_GROUP "${group}")
  set_cpack_component(${project}_MANUAL_GROUP "${group}")

  set_cpack_component(${project}_DEVELOP_DISPLAY_NAME "${name} development files")
  set_cpack_component(${project}_RUNTIME_DISPLAY_NAME "${name}")
  set_cpack_component(${project}_MANUAL_DISPLAY_NAME "${name} documentation")

  set_cpack_component(${project}_DEVELOP_DESCRIPTION "${description}")
  set_cpack_component(${project}_RUNTIME_DESCRIPTION "${description}")
  set_cpack_component(${project}_MANUAL_DESCRIPTION "${description}")

  # Debian
  string(REPLACE "_" "-" debian_name "${project}")
  if(is_tool)
    set(prefix "boost-")
  else()
    set(prefix "libboost-")
  endif()

  set_cpack_component(${project}_DEVELOP_DEB_PACKAGE "${prefix}${debian_name}-dev")
  set_cpack_component(${project}_RUNTIME_DEB_PACKAGE "${prefix}${debian_name}")
  set_cpack_component(${project}_MANUAL_DEB_PACKAGE  "${prefix}${debian_name}-doc")

  cmake_parse_arguments(DEB "" "" "DEV;LIB" ${BOOST_PROJECT_${project}_DEB_DEPENDS})
  list(APPEND CPACK_DEBIAN_BUILD_DEPENDS ${DEB_DEV} ${DEB_UNPARSED_ARGUMENTS})

  set_cpack_component(${project}_DEVELOP_DEBIAN_DEPENDS ${DEB_DEV})
  set_cpack_component(${project}_RUNTIME_DEBIAN_DEPENDS ${DEB_LIB} ${DEB_UNPARSED_ARGUMENTS})

  # dependencies
  set(develop_depends)
  set(runtime_depends)

  if(has_develop AND has_runtime)
    list(APPEND develop_depends ${project}_runtime)
  endif(has_develop AND has_runtime)

  foreach(dependancy ${BOOST_PROJECT_${project}_DEPENDS})
    if(has_develop AND BOOST_PROJECT_${dependancy}_HAS_DEVELOP)
      list(APPEND develop_depends ${dependancy}_develop)
    endif(has_develop AND BOOST_PROJECT_${dependancy}_HAS_DEVELOP)
    if(has_runtime AND BOOST_PROJECT_${dependancy}_HAS_RUNTIME)
      list(APPEND runtime_depends ${dependancy}_runtime)
    endif(has_runtime AND BOOST_PROJECT_${dependancy}_HAS_RUNTIME)
  endforeach(dependancy)

  set_cpack_component(${project}_DEVELOP_DEPENDS "${develop_depends}")
  set_cpack_component(${project}_RUNTIME_DEPENDS "${runtime_depends}")

  foreach(component develop runtime manual)
    if(has_${component})
      list(APPEND CPACK_COMPONENTS_ALL ${project}_${component})
    endif(has_${component})
  endforeach(component)
endforeach(project)

list(INSERT BOOST_FOOTER 0
  "${PROJECT_SOURCE_DIR}/BoostCPack.cmake"
  CPack
  )

foreach(file ${BOOST_FOOTER})
  include("${file}")
endforeach(file)
